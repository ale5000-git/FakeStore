/*
 * SPDX-FileCopyrightText: 2015 microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.1'
    }
}

def versionCode() {
    def stdout = new ByteArrayOutputStream()
    exec { commandLine 'git', 'rev-list', '--count', "HEAD"; standardOutput = stdout }
    return Integer.parseInt(stdout.toString().trim()) + 83700000
}

def versionName() {
    def stdout = new ByteArrayOutputStream()
    if (rootProject.file("gradlew").exists())
        exec { commandLine 'git', 'describe', '--tags', '--always', '--dirty'; standardOutput = stdout }
    else // automatic build system, don't tag dirty
        exec { commandLine 'git', 'describe', '--tags', '--always'; standardOutput = stdout }
    return stdout.toString().trim().substring(1)
}

private void configureSigning(def android, File keystorePropsFile)
{
    if (!keystorePropsFile.exists()) return
    if (android == null) throw new GradleException('android is null inside configureSigning()')

    println 'Signed build'

    Properties keystoreProps = new Properties()
    keystoreProps.load(new FileInputStream(keystorePropsFile))

    String keyStorePassword
    boolean fallbackToEnv = keystoreProps.containsKey('fallbackToEnv') && keystoreProps['fallbackToEnv'] == 'true'

    if (keystoreProps.containsKey('keyStorePassword'))
        keyStorePassword = keystoreProps['keyStorePassword']
    else if (fallbackToEnv)
        keyStorePassword = System.getenv('KEYSTORE_PASSWORD')

    if (keyStorePassword == null || keyStorePassword.isEmpty())
        throw new InvalidUserDataException('Keystore password is empty')

    android.signingConfigs {
        config {
            storeFile = rootProject.file(keystoreProps['storeFile'])
            storePassword = keyStorePassword
            keyAlias = keystoreProps['keyAlias']
            keyPassword = keyStorePassword
        }
    }

    android.buildTypes.release.signingConfig android.signingConfigs.config
}

allprojects {
    apply plugin: 'idea'
    version = versionName()

    ext.androidBuildVersionTools = "30.0.3"

    ext.appVersionCode = versionCode()
    ext.androidCompileSdk = 30
    ext.androidTargetSdk = 24
    ext.androidMinSdk = 9
}

subprojects {
    group = 'org.microg'
    repositories {
        mavenCentral()
        google()
    }
}
